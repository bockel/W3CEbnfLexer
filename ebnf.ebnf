/* W3C EBNF description of the W3C EBNF grammar */
/* Source: http://railroad.my28msec.com */

Grammar              ::= Production*
Production           ::= NCName '::=' ( Choice | Link )
NCName               ::= /* [http://www.w3.org/TR/xml-names/#NT-NCName] */
Choice               ::= SequenceOrDifference ( '|' SequenceOrDifference )*
SequenceOrDifference ::= ( Item ( '-' Item | Item* ))?
Item                 ::= Primary ( '?' | '*' | '+' )?
Primary              ::= NCName | StringLiteral | CharCode | CharClass | '(' Choice ')'
StringLiteral        ::= '"' [^"]* '"' | "'" [^']* "'"
CharCode             ::= '#x' [0-9a-fA-F]+
CharClass            ::= '[' '^'? ( Char | CharCode | CharRange | CharCodeRange )+ ']'
Char                 ::= /* [http://www.w3.org/TR/xml#NT-Char] */
CharRange            ::= Char '-' ( Char - ']' )
CharCodeRange        ::= CharCode '-' CharCode
Whitespace           ::= S | Comment
S                    ::= #x9 | #xA | #xD | #x20
Comment              ::= '/*' ( [^*] | '*'+ [^*/] )* '*'* '*/'
